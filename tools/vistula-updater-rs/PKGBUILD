# Maintainer: Patryk <patryk@vistulaos.dev>
pkgname=vistula-updater-rs
pkgver=1.3.0
pkgrel=1
pkgdesc="System updater (pacman) and Flatpak store for VistulaOS (Rust version)"
arch=('x86_64')
url="https://github.com/VistulaOS/vistula-updater-rs"
license=('GPL3')
depends=('pacman-contrib' 'flatpak' 'libnotify')
makedepends=('rust' 'cargo')
provides=('vistula-updater')
conflicts=('vistula-updater')

source=("${pkgname}-${pkgver}.tar.gz")
sha256sums=('SKIP')

build() {
    cd "${pkgname}-${pkgver}"
    cargo build --release --locked
}

check() {
    cd "${pkgname}-${pkgver}"
    cargo test --release --locked
}

package() {
    cd "${pkgname}-${pkgver}"
    
    # Binaries
    install -Dm755 target/release/vistula-updater "${pkgdir}/usr/bin/vistula-updater"
    install -Dm755 target/release/vistula-updater-notifier "${pkgdir}/usr/bin/vistula-updater-notifier"
    
    # Desktop files
    install -Dm644 packaging/vistula-updater.desktop "${pkgdir}/usr/share/applications/vistula-updater.desktop"
    install -Dm644 packaging/vistula-updater-notifier.desktop "${pkgdir}/etc/xdg/autostart/vistula-updater-notifier.desktop"
    
    # i18n assets
    install -Dm644 assets/i18n/en.json "${pkgdir}/usr/share/vistula-updater/assets/i18n/en.json"
    install -Dm644 assets/i18n/pl.json "${pkgdir}/usr/share/vistula-updater/assets/i18n/pl.json"
}
